import qs
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Qt5Compat.GraphicalEffects
import Qt.labs.synchronizer
import qs.modules.common.functions
import "phone"

Item {
    id: root
    required property var scopeRoot
    property int sidebarPadding: 12
    anchors.fill: parent
    property var visitedTabs: ({})

    // Toggles from Config
    property bool aiChatEnabled: Config.options.policies.ai !== 0
    property bool translatorEnabled: Config.options.policies.translator !== 0
    property bool mediaEnabled: Config.options.policies.player !== 0
    property bool wallpapersEnabled: Config.options.policies.wallpapers !== 0
    property bool animeEnabled: Config.options.policies.weeb !== 0
    property bool animeCloset: Config.options.policies.weeb === 2

    // Tab and Page mapping
    property var tabs: [
        {
            icon: "neurology",
            name: Translation.tr("Intelligence"),
            enabled: root.aiChatEnabled,
            component: aiChat
        },
        {
            icon: "translate",
            name: Translation.tr("Translator"),
            enabled: root.translatorEnabled,
            component: translator
        },
        {
            icon: "music_note",
            name: Translation.tr("Media"),
            enabled: root.mediaEnabled,
            component: media
        },
        {
            icon: "wallpaper",
            name: Translation.tr("Wallpapers"),
            enabled: root.wallpapersEnabled,
            component: wallpaperBrowser
        },
        {
            icon: "bookmark_heart",
            name: Translation.tr("Anime"),
            enabled: root.animeEnabled && !root.animeCloset,
            component: anime
        },
        {
            icon: "smartphone",
            name: Translation.tr("Phone"),
            enabled: Config.options.policies.phone !== 0,
            component: phonePlaceholder
        }
    ]

    property var activeTabs: tabs.filter(t => t.enabled)
    property var tabButtonList: activeTabs.map(t => ({
                icon: t.icon,
                name: t.name
            }))
    property int tabCount: activeTabs.length
    // Holds the previously-focused tab index so the bounce-in animation
    // (mirroring the Cheatsheet tab transition) knows the direction.
    property int _prevTabIndex: Persistent.states.sidebar.policies.tab
    Component.onCompleted: {
        root._prevTabIndex = Persistent.states.sidebar.policies.tab;
    }

    function validateTabIndex() {
        if (!Persistent.ready)
            return;
        var t = Persistent.states.sidebar.policies.tab;
        if (tabCount > 0) {
            if (t < 0 || t >= tabCount) {
                Persistent.states.sidebar.policies.tab = 0;
            }
        } else {
            if (t !== 0) {
                Persistent.states.sidebar.policies.tab = 0;
            }
        }
    }

    onActiveTabsChanged: {
        root.validateTabIndex();
    }

    Connections {
        target: Persistent
        function onReadyChanged() {
            root.validateTabIndex();
        }
    }

    Connections {
        target: Persistent.states.sidebar.policies
        ignoreUnknownSignals: true
        function onTabChanged() {
            root.validateTabIndex();
        }
    }

    Connections {
        target: GlobalStates
        function onSidebarLeftOpenChanged() {
            if (!GlobalStates.sidebarLeftOpen) {
                root.visitedTabs = {};
            }
            if (GlobalStates.sidebarLeftOpen) {
                if ((Config.options?.appearance?.animationMultiplier ?? 1.0) <= 0.25) {
                    toolbarContainer.opacity = 1
                    toolbarTrans.x = 0
                    tabBar.opacity = 1
                    tabBarTrans.x = 0
                    return;
                }
                toolbarContainer.opacity = 0
                toolbarTrans.x = -80
                tabBar.opacity = 0
                tabBarTrans.x = -30
                
                toolbarEntranceAnim.stop()
                toolbarEntranceAnim.start()

                if (swipeView.currentItem?.item && typeof swipeView.currentItem.item.triggerContentEntrance === "function") {
                    swipeView.currentItem.item.triggerContentEntrance();
                }
            }
        }
    }

    ParallelAnimation {
        id: toolbarEntranceAnim

        // Clean slide-in of navbar container from left-to-right (-80 -> 0)
        SequentialAnimation {
            PauseAnimation { duration: 30 }
            ParallelAnimation {
                NumberAnimation { target: toolbarContainer; property: "opacity"; to: 1.0; duration: 280; easing.type: Easing.OutCubic }
                NumberAnimation { target: toolbarTrans; property: "x"; to: 0; duration: 360; easing.type: Easing.OutCubic }
            }
        }

        // Staggered slide-in of tab buttons inside the navbar
        SequentialAnimation {
            PauseAnimation { duration: 90 }
            ParallelAnimation {
                NumberAnimation { target: tabBar; property: "opacity"; to: 1.0; duration: 250; easing.type: Easing.OutCubic }
                NumberAnimation { target: tabBarTrans; property: "x"; to: 0; duration: 320; easing.type: Easing.OutCubic }
            }
        }
    }

    function focusActiveItem() {
        if (swipeView.currentItem && swipeView.currentItem.item) {
            swipeView.currentItem.item.forceActiveFocus();
        }
    }

    Keys.onPressed: event => {
        if (event.modifiers === Qt.ControlModifier) {
            if (event.key === Qt.Key_PageDown) {
                swipeView.incrementCurrentIndex();
                event.accepted = true;
            } else if (event.key === Qt.Key_PageUp) {
                swipeView.decrementCurrentIndex();
                event.accepted = true;
            }
        }
    }

    ColumnLayout {
        // Clip to the sidebar bounds. Without this, the Toolbar (with
        // Layout.alignment: Qt.AlignHCenter) overflows visibly past the sidebar
        // edges during width transitions, when the tab count changes at runtime,
        // or when translated strings are wider than the English defaults.
        clip: true
        anchors {
            fill: parent
            leftMargin: sidebarPadding
            rightMargin: sidebarPadding
            bottomMargin: sidebarPadding
            topMargin: sidebarPadding
        }
        spacing: sidebarPadding

        Item {
            id: toolbarContainer
            visible: activeTabs.length > 1
            Layout.alignment: Qt.AlignHCenter
            Layout.preferredHeight: mainToolbar.implicitHeight
            Layout.maximumWidth: parent.width - sidebarPadding * 2
            Layout.preferredWidth: Math.min(mainToolbar.implicitWidth, parent.width - sidebarPadding * 2)

            transform: Translate {
                id: toolbarTrans
                x: 0
            }

            Toolbar {
                id: mainToolbar
                anchors.fill: parent
                enableShadow: false
                colBackground: Appearance.colors.colLayer3
                ToolbarTabBar {
                    id: tabBar
                    Layout.alignment: Qt.AlignHCenter
                    tabButtonList: root.tabButtonList
                    currentIndex: Persistent.states.sidebar.policies.tab

                    transform: Translate {
                        id: tabBarTrans
                        x: 0
                    }

                    onCurrentIndexChanged: {
                        if (currentIndex >= 0 && currentIndex < root.tabCount && Persistent.states.sidebar.policies.tab !== currentIndex) {
                            Persistent.states.sidebar.policies.tab = currentIndex;
                        }
                    }
                }
            }
        }

        Rectangle {
            Layout.fillWidth: true
            Layout.fillHeight: true
            radius: Appearance.rounding.normal
            color: "transparent"

            SwipeView {
                id: swipeView
                anchors.fill: parent
                spacing: 10
                
                onCountChanged: {
                    if (count > 0 && Persistent.states.sidebar.policies.tab >= 0 && Persistent.states.sidebar.policies.tab < count) {
                        currentIndex = Persistent.states.sidebar.policies.tab;
                    }
                }
                
                Connections {
                    target: Persistent.states.sidebar.policies
                    function onTabChanged() {
                        if (swipeView.currentIndex !== Persistent.states.sidebar.policies.tab && Persistent.states.sidebar.policies.tab >= 0 && Persistent.states.sidebar.policies.tab < swipeView.count) {
                            swipeView.currentIndex = Persistent.states.sidebar.policies.tab;
                        }
                    }
                }

                onCurrentIndexChanged: {
                    if (currentIndex >= 0 && currentIndex < root.tabCount && Persistent.states.sidebar.policies.tab !== currentIndex) {
                        Persistent.states.sidebar.policies.tab = currentIndex;
                    }
                    Qt.callLater(() => {
                        root._prevTabIndex = currentIndex;
                    });
                    
                    if (currentIndex >= 0) {
                        var visited = root.visitedTabs;
                        if (!visited[currentIndex]) {
                            visited[currentIndex] = true;
                            root.visitedTabs = visited;
                        }
                    }

                    if (swipeView.currentItem?.item && typeof swipeView.currentItem.item.triggerContentEntrance === "function") {
                        swipeView.currentItem.item.triggerContentEntrance();
                    }
                }

                Component.onCompleted: {
                    if (contentItem) {
                        contentItem.highlightMoveDuration = 0;
                    }
                    if (count > 0 && Persistent.states.sidebar.policies.tab >= 0 && Persistent.states.sidebar.policies.tab < count) {
                        currentIndex = Persistent.states.sidebar.policies.tab;
                    }
                    var visited = root.visitedTabs;
                    visited[currentIndex] = true;
                    root.visitedTabs = visited;
                }

                implicitWidth: Math.max.apply(null, contentChildren.map(child => child.implicitWidth || 0))
                implicitHeight: Math.max.apply(null, contentChildren.map(child => child.implicitHeight || 0))

                clip: true

                Repeater {
                    model: root.activeTabs
                    Loader {
                        id: tabDelegate
                        required property var modelData
                        required property int index

                        active: (GlobalStates.sidebarLeftOpen && (SwipeView.isCurrentItem || !!root.visitedTabs[index]))
                                || (modelData.icon === "smartphone" && (GlobalStates.phoneMicRunning || GlobalStates.phoneCameraRunning))
                        sourceComponent: modelData.component

                        transform: Translate {
                            id: trans
                            x: 0
                        }

                        onLoaded: {
                            if (item)
                                item.anchors.fill = this;
                        }

                        readonly property bool isCurrent: swipeView.currentIndex === index
                        onIsCurrentChanged: {
                            if (isCurrent) {
                                const diff = index - root._prevTabIndex;
                                if (diff !== 0) {
                                    bounceAnim.stop();
                                    opacityAnim.stop();
                                    trans.x = diff > 0 ? 120 : -120;
                                    tabDelegate.opacity = 0;
                                    bounceAnim.start();
                                    opacityAnim.start();
                                }
                                // Trigger entrance animation for the tab content
                                Qt.callLater(function() {
                                    if (tabDelegate.item && typeof tabDelegate.item.triggerContentEntrance === "function") {
                                        tabDelegate.item.triggerContentEntrance();
                                    }
                                });
                            } else {
                                tabDelegate.opacity = 1;
                                trans.x = 0;
                            }
                        }

                        NumberAnimation {
                            id: bounceAnim
                            target: trans
                            property: "x"
                            to: 0
                            duration: 420
                            easing.type: Easing.OutBack
                            easing.overshoot: 1.45
                        }

                        NumberAnimation {
                            id: opacityAnim
                            target: tabDelegate
                            property: "opacity"
                            from: 0
                            to: 1
                            duration: 280
                            easing.type: Easing.OutCubic
                        }
                    }
                }
            }

            // Show placeholder if no tabs are active
            Loader {
                anchors.fill: parent
                active: root.activeTabs.length === 0
                sourceComponent: placeholder
            }
        }

        Component {
            id: aiChat
            AiChat {}
        }
        Component {
            id: translator
            Translator {}
        }
        Component {
            id: media
            SidebarPlayerControl {}
        }
        Component {
            id: wallpaperBrowser
            WallpaperBrowserUI {}
        }
        Component {
            id: anime
            Anime {}
        }
        Component {
            id: phonePlaceholder
            Phone {}
        }
        Component {
            id: placeholder
            Item {
                StyledText {
                    anchors.centerIn: parent
                    text: root.animeCloset ? Translation.tr("Nothing") : Translation.tr("Enjoy your empty sidebar...")
                    color: Appearance.colors.colSubtext
                }
            }
        }
    }
}
