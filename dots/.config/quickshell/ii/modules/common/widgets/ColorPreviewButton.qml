import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import qs.modules.common.functions

RippleButton {
    id: root

    readonly property string builtInThemeDirectory: Directories.defaultThemes
    readonly property string customThemeDirectory: Directories.customThemes

    property string colorScheme: "scheme-auto"
    property string colorSchemeDisplayName: ""

    property bool builtInTheme: false
    readonly property string builtInThemeFilePath: builtInThemeDirectory + "/" + colorScheme + ".json"
    readonly property string builtInThemeCommand: `jq -r '.primary, .primary_container, .secondary' ${builtInThemeFilePath}`

    property bool customTheme: false
    readonly property string customThemeFilePath: customThemeDirectory + "/" + colorScheme + ".json"
    readonly property string customThemeCommand: `jq -r '.primary, .primary_container, .secondary' ${customThemeFilePath}`

    readonly property string wallpaperPath: (Config.options && Config.options.background && Config.options.background.wallpaperPath)
        ? Config.options.background.wallpaperPath : ""
    readonly property string activeWallpaperPath: {
        if (Config.options && Config.options.background && Config.options.background.useWallpaperEngine)
            return "/tmp/wpe_screenshot.png";
        return wallpaperPath;
    }
    readonly property string scriptPath: FileUtils.trimFileProtocol(
        `${Directories.scriptPath}/colors/generate_colors_material.py`)
    readonly property string resolvedScheme: colorScheme === "scheme-auto"
        ? "scheme-tonal-spot" : colorScheme
    readonly property string fullCommand: activeWallpaperPath !== ""
        ? `${scriptPath} --path "${activeWallpaperPath}" --scheme ${resolvedScheme} --preview`
        : ""

    // Widget color previews receive their colors directly and do not need a
    // process. Keep this interface compatible with WidgetsConfig.qml.
    property color previewPrimary: "transparent"
    property color previewSecondary: "transparent"
    property color previewTertiary: "transparent"
    property bool usePreviewColors: false
    property color primaryColor: usePreviewColors ? previewPrimary : "transparent"
    property color secondaryColor: usePreviewColors ? previewSecondary : "transparent"
    property color tertiaryColor: usePreviewColors ? previewTertiary : "transparent"

    property bool loaded: usePreviewColors
    property bool shouldLoad: false

    property bool isWidgetScheme: false
    property bool widgetSchemeToggled: false
    readonly property bool toggled: isWidgetScheme
        ? widgetSchemeToggled
        : Config.options.appearance.palette.type === colorScheme
    property bool expressiveSelection: false
    readonly property bool sharpMode: Config.options.appearance.sharpMode

    colBackground: toggled ? Appearance.colors.colPrimaryContainer : Appearance.colors.colLayer2
    colBackgroundHover: toggled
        ? Appearance.colors.colPrimaryContainerHover
        : Appearance.colors.colLayer2Hover
    colRipple: toggled
        ? Appearance.colors.colPrimaryContainerActive
        : Appearance.colors.colLayer2Active

    buttonRadius: Appearance.rounding.small

    scale: (root.down ? 0.96 : (root.hovered ? 1.01 : 1.0))
        * (root.expressiveSelection && root.toggled ? 1.03 : 1.0)

    Layout.fillWidth: true
    implicitHeight: 64

    onClicked: {
        if (isWidgetScheme)
            return;

        if (customTheme) {
            Config.options.appearance.palette.type = colorScheme;
            const themePath = FileUtils.trimFileProtocol(customThemeFilePath);
            const targetPath = FileUtils.trimFileProtocol(Directories.generatedMaterialThemePath);
            const script = FileUtils.trimFileProtocol(
                `${Directories.scriptPath}/colors/recolor_icons.py`);
            Quickshell.execDetached([
                "bash", "-c", `cp "${themePath}" "${targetPath}" && python3 "${script}"`
            ]);
        } else if (builtInTheme) {
            Config.options.appearance.palette.type = colorScheme;
            const themePath = FileUtils.trimFileProtocol(builtInThemeFilePath);
            const targetPath = FileUtils.trimFileProtocol(Directories.generatedMaterialThemePath);
            const script = FileUtils.trimFileProtocol(
                `${Directories.scriptPath}/colors/recolor_icons.py`);
            Quickshell.execDetached([
                "bash", "-c", `cp "${themePath}" "${targetPath}" && python3 "${script}"`
            ]);
        } else {
            Config.options.appearance.palette.type = colorScheme;
            Config.options.appearance.palette.accentColor = "";
            Config.saveOptionsNow();
            Quickshell.execDetached(["bash", "-c", `${Directories.wallpaperSwitchScriptPath} --noswitch`]);
        }
    }

    readonly property string effectiveCommand: customTheme
        ? customThemeCommand
        : builtInTheme ? builtInThemeCommand : fullCommand

    function startColorFetch() {
        if (shouldLoad && !loaded && effectiveCommand !== "")
            colorFetchProcess.running = true;
    }

    onShouldLoadChanged: root.startColorFetch()

    onWallpaperPathChanged: {
        if (shouldLoad && effectiveCommand !== "") {
            loaded = false;
            colorFetchProcess.running = true;
        }
    }

    readonly property string wpeId: (Config.options && Config.options.background)
        ? Config.options.background.wallpaperEngineId : ""
    onWpeIdChanged: {
        if (shouldLoad && effectiveCommand !== "") {
            loaded = false;
            colorFetchProcess.running = true;
        }
    }

    property bool useWpe: (Config.options && Config.options.background)
        ? Config.options.background.useWallpaperEngine : false
    onUseWpeChanged: {
        if (shouldLoad && effectiveCommand !== "") {
            loaded = false;
            colorFetchProcess.running = true;
        }
    }

    Process {
        id: colorFetchProcess
        running: false
        command: ["bash", "-c", root.effectiveCommand]

        stdout: StdioCollector {
            onStreamFinished: {
                try {
                    if (root.customTheme || root.builtInTheme) {
                        const colors = this.text.trim().split("\n");
                        root.primaryColor = colors[0] || "transparent";
                        root.secondaryColor = colors[1] || "transparent";
                        root.tertiaryColor = colors[2] || "transparent";
                    } else {
                        const data = JSON.parse(this.text);
                        root.primaryColor = data.primary || "transparent";
                        root.secondaryColor = data.primary_container || "transparent";
                        root.tertiaryColor = data.secondary || "transparent";
                    }

                    root.loaded = true;
                    myCanvas.requestPaint();
                } catch (error) {
                    console.warn("[ColorPreviewButton] Preview parse failed:", error);
                }
            }
        }
    }

    StyledToolTip {
        text: root.colorSchemeDisplayName
    }

    Item {
        anchors.fill: parent

        StyledText {
            anchors.fill: parent
            visible: !root.loaded
            elide: Text.ElideRight
            text: root.colorSchemeDisplayName
            horizontalAlignment: Text.AlignHCenter
            color: Appearance.colors.colOnPrimaryContainer
            font.pixelSize: Appearance.font.pixelSize.small
        }

        Canvas {
            id: myCanvas
            anchors.centerIn: parent
            anchors.margins: 8
            implicitWidth: root.implicitHeight - 16
            implicitHeight: root.implicitHeight - 16
            antialiasing: true

            onPaint: {
                const ctx = getContext("2d");
                const centerX = width / 2;
                const centerY = height / 2;
                const radius = width / 2;

                ctx.reset();

                if (root.sharpMode) {
                    ctx.fillStyle = root.primaryColor;
                    ctx.fillRect(0, 0, width, centerY);

                    ctx.fillStyle = root.secondaryColor;
                    ctx.fillRect(centerX, centerY, centerX, centerY);

                    ctx.fillStyle = root.tertiaryColor;
                    ctx.fillRect(0, centerY, centerX, centerY);
                } else {
                    ctx.beginPath();
                    ctx.fillStyle = root.primaryColor;
                    ctx.moveTo(centerX, centerY);
                    ctx.arc(centerX, centerY, radius, Math.PI, 0, false);
                    ctx.fill();

                    ctx.beginPath();
                    ctx.fillStyle = root.secondaryColor;
                    ctx.moveTo(centerX, centerY);
                    ctx.arc(centerX, centerY, radius, 0, Math.PI / 2, false);
                    ctx.fill();

                    ctx.beginPath();
                    ctx.fillStyle = root.tertiaryColor;
                    ctx.moveTo(centerX, centerY);
                    ctx.arc(centerX, centerY, radius, Math.PI / 2, Math.PI, false);
                    ctx.fill();
                }
            }
        }
    }
}
